
require:
   inquirer-shortcuts as inq
   chalk
   .index ->
      lang-defaults


async quaint-setup(config, instructions) =
   url = "https://github.com/breuleux/quaint-smartypants"
   print 'Plugin documentation: {chalk.bold.cyan(url)}'

   data = config.plugins["smartypants"] or= {=}

   data.locale = await inq.list{
      "What locale to use for translation?"
      {.en, .fr}
      {default = 0}
   }

   defaults = lang-defaults[data.locale]

   feature-list = await inq.checkbox{
      "Select the replacement rules you want:"
      {{value = 'backtickQuotes'
        name = "Latex-style quotes:   ``text'' -> “text”"
        checked = defaults.backtick-quotes}
       {value = 'angleQuotes'
        name = "Angle bracket quotes: <<text>> -> “text”"
        checked = defaults.angle-quotes}
       {value = 'quotes'
        name = "Plain double quotes:  \"text\" -> “text”"
        checked = defaults.quotes}
       {value = 'singleQuotes'
        name = "Single quotes:        'text' -> ‘text’"
        checked = defaults.single-quotes}
       {value = 'enDash'
        name = "En dash:              -- -> –"
        checked = defaults.en-dash}
       {value = 'emDash'
        name = "Em dash:              --- -> —  (wider than en dash)"
        checked = defaults.em-dash}
       {value = 'ellipsis'
        name = "Ellipsis:             ... -> …"
        checked = defaults.ellipsis}
       {value = 'asterism'
        name = "Asterism:             *** -> ⁂"
        checked = defaults.asterism}
       {value = 'spacePunctuation'
        name = "Space before !?:      Hello! -> Hello !"
        checked = defaults.space-punctuation}
       {value = 'oe'
        name = "Œ ligature            Coeur -> Cœur"
        checked = defaults.oe}
      }
   }

   data.features = object with 'backtickQuotes/angleQuotes/quotes/singleQuotes/enDash/emDash/ellipsis/asterism/spacePunctuation/oe'.split{"/"} each name -> {name, name in feature-list}


provide = quaint-setup
